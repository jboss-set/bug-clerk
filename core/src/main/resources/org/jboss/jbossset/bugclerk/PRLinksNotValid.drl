/*
 * JBoss, Home of Professional Open Source.
 * Copyright 2015, Red Hat, Inc., and individual contributors
 * as indicated by the @author tags. See the copyright.txt file in the
 * distribution for a full listing of individual contributors.
 *
 * This is free software; you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation; either version 2.1 of
 * the License, or (at your option) any later version.
 *
 * This software is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this software; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */

package org.jboss.jbossset.bugclerk;

import org.jboss.set.aphrodite.issue.trackers.jira.JiraIssue;
import org.jboss.set.aphrodite.domain.Violation;
import org.jboss.jbossset.bugclerk.checks.GitHelper;
import org.jboss.set.aphrodite.domain.Stream;

import java.net.URL;
import java.util.List;

global org.jboss.jbossset.bugclerk.aphrodite.AphroditeClient aphrodite;

rule "PRLinksNotValid"
  salience 0
  dialect "mvel"
  when
    $candidate : Candidate( $jiraIssue: bug#JiraIssue, bug#JiraIssue.getPullRequests() != null, bug#JiraIssue.getPullRequests().isEmpty() != true, filtered == false )
    $stream : Stream() from GitHelper.getStreamForIssue($jiraIssue, aphrodite);
    $list : List() from collect (String() from GitHelper.getIncorrectPRs($jiraIssue, $stream));
    eval( ! $list.isEmpty() )
  then
    $candidate.addViolation(new Violation("PRLinksNotValid",
        "Following PRs "+String.join(", ", $list)+" don't belong to the expected list of repositoires in '"+$stream.getName()+
        "' stream specified in https://raw.githubusercontent.com/jboss-set/jboss-streams/master/streams.json",
    Severity.MINOR));
end

rule "PRLinksNoStreamsFound"
  salience 0
  dialect "mvel"
  when
    $candidate : Candidate( $jiraIssue: bug#JiraIssue, bug#JiraIssue.getPullRequests() != null, bug#JiraIssue.getPullRequests().isEmpty() != true, filtered == false )
    $stream : Stream() from GitHelper.getStreamForIssue($jiraIssue, aphrodite);
    eval($stream.getName().equals("none"))
  then
    $candidate.addViolation(new Violation("PRLinksNoStreamsFound",
         "No valid streams was found for this issue in the https://raw.githubusercontent.com/jboss-set/jboss-streams/master/streams.json.",
    Severity.MINOR));
end
